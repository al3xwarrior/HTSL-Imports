stat "joins" inc 1
globalstat "joins" inc 1

if (stat "bot" = 1) {
    globalstat "bot/online" set 1
}

chat "&r"
chat "&6&m-----------------------------------------"
chat "&b✦ &eWelcome to &7﴾ %house.name% &7﴿"
chat "&r"
chat "&b✦ &ePurchase Kits, Explore the Map, and more!"
chat "&6&m-----------------------------------------"
sound "entity.experience_orb.pickup" 0.7 1.0 "invokers_location"

tp "house_spawn"

// Staff Alerts
if (hasGroup "T-Mod" true) {
    stat "AC/alerts" set 1
    chat "&7[&4&lHAC&7] &fAnti-Cheat alerts &aEnabled&f! Toggle with &7&l/alerts"
}

// Fix Event Teams
if or (hasTeam "Blue", hasTeam "Red", hasTeam "Juggernaut") {
    setTeam "None"
} 

// Player IDs
if (stat "playerid" = 0) {
    globalstat "playerids" inc 1
    stat "playerid" set "%stat.global/playerids%"
    globalstat "idforbroadcast" set "%stat.player/playerid%"
    globalstat "temp" set 1
    function "JoinQuit" true
} else {
    globalstat "idforbroadcast" set "%stat.player/playerid%"
    globalstat "temp" set 2
    function "JoinQuit" true
}

// Fixed local multiplier
if (stat "goldmulti" = 0) {
    stat "goldmulti" set 1
}
// Fix Ability Slots
if or (stat "weaponslot" = 0, stat "abilityslot" = 0) {
    stat "weaponslot" set 1
    stat "abilityslot" set 2
}

resetInventory

function "fixKitStats"

// Combat Logging
if (stat "leftincombat" = 1) {
    pause 30
    
    sound "entity.cat.hurt" 0.7 0.5 "invokers_location"
    stat "leftincombat" set 0
    stat "gold" dec 20
    stat "xpneeded" inc 100
    chat "&7&m-------------------------------------------"
    chat "&r &r &r &4&lCombat Logging Warning"
    chat "&0"
    chat "&cIn your last fight, you left in Combat! &620⛃ &cremoved and &3100✥ XP added!"
    chat "&7&m-------------------------------------------"
}

if (stat "playtimeearn" = 0) {
    stat "playtimeearn" set 2
}