// Stop if Practicing
if (inRegion "PracticeRoom", inRegion "PracticePool") {
    exit
}

// Already converting to Infected
if (hasTeam "Survivor", hasPotion "Regeneration", hasPotion "Resistance", hasPotion "Slowness") {
    cancelEvent
    exit
}

// Changing to Survivor
if (hasTeam "Survivor", hasPotion "Regeneration", hasPotion "Slowness", hasPotion "Resistance") {
    cancelEvent
    exit
}

// Cured from Infection
if (damageCause "Fire Tick", hasTeam "Infected") {
    cancelEvent
    setTeam "Survivor"
    applyPotion "Regeneration" 2 10 true
    applyPotion "Resistance" 2 10 true
    applyPotion "Slowness" 2 10 true
    fullHeal

    sound "entity.zombie.step" 0.7 1.2 "invokers_location"
    title "&d&lCU" "&0" 0 1 1
    pause 4
    title "&d&lCURE" "&0" 0 1 1
    pause 4
    sound "entity.zombie.step" 0.7 1.6 "invokers_location"
    title "&d&lCURED" "&0" 0 1 1
    pause 35
    sound "entity.zombie.infect" 0.7 2.0 "invokers_location"
    title "&d&lCURED" "&eRun!" 0 2 1
    applyPotion "Slowness" 2000000 1 true
    applyLayout "Survivor"
    exit
}

// Cancel Thorns but set IDs + Anticheat Attacker + Attack Perks
if (damageCause "Thorns", globalstat "ac/washit" = 1) {
    cancelEvent
    function "attackPerks" false
    function "HitSounds" false
    stat "combo" inc 1
    globalstat "ac/washit" set 0
    globalstat "ac/attackerx" set "%player.location.x%"
    globalstat "ac/attackery" set "%player.location.y%"
    globalstat "ac/attackerz" set "%player.location.z%"
    globalstat "ac/attackerpit" set "%player.location.pitch%"
    globalstat "ac/attackeryaw" set "%player.location.yaw%"
    function "AC_ReachCheck" false
    globalstat "raycast_radius" set 2
    globalstat "range" set 7
    globalstat "raycastid" set 1
    function "raycast" false
    function "PyroSword1" false
} else {
    stat "combo" set 0
    globalstat "victimid" set "%stat.player/playerid%"
}

// Anticheat Victim
if (damageCause "Entity Attack") {
    globalstat "ac/washit" set 1
    globalstat "ac/victimx" set "%player.location.x%"
    globalstat "ac/victimy" set "%player.location.y%"
    globalstat "ac/victimz" set "%player.location.z%"
    globalstat "ac/victimpit" set "%player.location.pitch%"
    globalstat "ac/victimyaw" set "%player.location.yaw%"
} else {
    globalstat "ac/washit" set 0
}

// Victim in Spawn
if (damageCause "Entity Attack", damageAmount >= 3, inRegion "Spawn") {
    cancelEvent
    function "checkerStats" false
    function "checkerMenu" true
}
// Cancel All Spawn Damage
if (inRegion "Spawn") {
    cancelEvent
    exit
}

// If doing /spawn
if (stat "spawning" = 1) {
    stat "spawning" set 0
    chat "&c&lYou got hit! Spawn teleport canceled!"
    sound "block.anvil.land" 0.7 1.0 "Invokers Location"
}

// Stomper Kit
if (damageCause "Fall", stat "stomping" = 1) {
    cancelEvent
    stat "stomping" set 0
    globalstat "xpos" set "%player.location.x%"
    globalstat "ypos" set "%player.location.y%"
    globalstat "zpos" set "%player.location.z%"
    globalstat "ability_id" set 1
    globalstat "ability_plrid" set "%stat.player/playerid%"

    function "GetNearbyPlayers" true
    exit
}

// TP Bow
if (damageCause "Projectile", damageAmount > 8, globalstat "tpbow/id" > 0) {
    cancelEvent
    globalstat "tpbow/vicx" set "%player.location.x%"
    globalstat "tpbow/vicy" set "%player.location.y%"
    globalstat "tpbow/vicz" set "%player.location.z%"
    function "TPBow" true
    exit
}

// Cancel if Safefall is active
if (stat "safefall" > 0, damageCause "Fall") {
    cancelEvent
    exit
}

// Hacks
if (damageCause "Entity Attack", stat "h/velocity" > 0) {
    stat "temp" set "%random.int/0 101%"
}
if (damageCause "Entity Attack", stat "h/velocity" > 0, stat "temp" <= "%stat.player/h/velocity%") {
    pause 2
    tp "custom_coordinates" "~0 ~0.2 ~0"
}

// Baker Kit
if (damageCause "Entity Attack", hasPotion "Instant Damage", stat "kit" = 13) {
    pause 2
    tp "custom_coordinates" "~0 ~0 ~0"
}

stat "combat" set 10